plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'maven'
    id 'maven-publish'
    id 'java-library'
}

group 'com.ringcentral'
version '0.6.3'

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.31'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.7.0'
    compile group: 'com.pubnub', name: 'pubnub-gson', version: '4.6.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'com/ringcentral/definitions/**',
                'com/ringcentral/paths/**'
            ])
        })
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "tylerlong"
            name "Tyler Liu"
            email "tyler.liu@ringcentral.com"
        }
    }
    scm {
        url "https://github.com/ringcentral/ringcentral-java"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.ringcentral'
            artifactId 'ringcentral'
            version '0.6.3'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'RingCentral Java SDK')
                root.appendNode('name', 'ringcentral')
                root.appendNode('url', 'https://github.com/ringcentral/ringcentral-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'ringcentral'
        userOrg = user
        desc = 'RingCentral Java SDK'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/ringcentral/ringcentral-java'
        issueTrackerUrl = 'https://github.com/ringcentral/ringcentral-java/issues'
        vcsUrl = 'https://github.com/ringcentral/ringcentral-java.git'
        labels = ['RingCentral']
        publicDownloadNumbers = true
        githubRepo = 'ringcentral/ringcentral-java' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        version {
            name = '0.6.3'
            desc = 'RingCentral Java SDK 0.6.3'
            vcsTag = '0.6.3'
            gpg {
                sign = true
            }
        }
    }
}
